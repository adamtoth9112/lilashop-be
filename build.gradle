plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.openapi.generator' version '7.10.0'
}

group = 'com.lilacode.lilashop'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(23)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'

	// Lombok
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	// Persistence
	runtimeOnly 'io.r2dbc:r2dbc-h2'
	runtimeOnly 'com.h2database:h2'
	implementation 'org.flywaydb:flyway-core'

	// Validation
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'

	// OpenAPI
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.2.0' // Swagger UI
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testCompileOnly 'org.projectlombok:lombok:1.18.30'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

sourceSets {
	main {
		java {
			srcDir "${buildDir}/generated/src/main/java"
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "${projectDir}/src/main/resources/openapi.yaml"
	outputDir = "${buildDir}/generated"
	apiPackage = "com.lilashop.api"
	modelPackage = "com.lilashop.api.model"
	configOptions = [
			interfaceOnly: 'true',
			useSpringBoot3: 'true',
			reactive: 'true',
			useJakartaEe: 'true'
	]
}

tasks.compileJava {
	dependsOn tasks.openApiGenerate
}

tasks.bootJar {
	archiveFileName = "lilashop-backend.jar" // Name of the JAR file
	destinationDirectory = file("$buildDir/libs") // Output directory
}